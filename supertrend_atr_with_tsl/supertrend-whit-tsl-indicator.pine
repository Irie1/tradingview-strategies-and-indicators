//@version=4
// Copyright (c) 2019-present, Mauricio Pimenta (exit490)

study("SuperTrend ATR with Trailing Stop Loss", overlay=true, precision = 8)

length = input(title="ATR Period", defval=1)
mult = input(title="ATR Multiplier", step=0.1, defval=3.0)

atr = mult * atr(length)

longStop = hl2 - atr
longStopPrev = nz(longStop[1], longStop)
longStop := close[1] > longStopPrev ? max(longStop, longStopPrev) : longStop

shortStop = hl2 + atr
shortStopPrev = nz(shortStop[1], shortStop)
shortStop := close[1] < shortStopPrev ? min(shortStop, shortStopPrev) : shortStop

dir = 1
dir := nz(dir[1], dir)
dir := dir == -1 and close > shortStopPrev ? 1 : dir == 1 and close < longStopPrev ? -1 : dir


longColor = color.blue
shortColor = color.blue

var valueToPlot = 0.0
var colorToPlot = color.white

if (dir == 1)
    valueToPlot := longStop
    colorToPlot := color.green
else
    valueToPlot := shortStop
    colorToPlot := color.red

//plot
plot(valueToPlot == 0.0 ? na : valueToPlot, title="BuyLine", linewidth=2, color=colorToPlot)

plotshape(dir == 1 and dir[1] == -1 ? longStop : na, title="Buy", style=shape.labelup, location=location.absolute, size=size.normal, text="Buy", transp=0, textcolor = color.white, color=color.green, transp=0)
plotshape(dir == -1 and dir[1] == 1 ? shortStop : na, title="Sell", style=shape.labeldown, location=location.absolute, size=size.normal, text="Sell", transp=0, textcolor = color.white, color=color.red, transp=0)

barcolor(dir == 1 ? color.green: color.red)

hasEntryLongConditional() => dir == 1
hasCloseLongConditional() => dir == -1

hasEntryShortConditional() => dir == -1
hasCloseShortConditional() => dir == 1

tradeType = input(defval="LONG", title="Trade Type ", options=["LONG", "SHORT"])
inputStopLossPercent = input(defval = 3.0, minval = 0.0)

stopLossPercent = tradeType == "LONG" ? inputStopLossPercent * -1 : inputStopLossPercent

var entryPrice = 0.0
var updatedEntryPrice = 0.0
var stopLossPrice = 0.0

var alertOpenPosition = false
var alertClosePosition = false

var hasOpenPosition = false

notHasOpenPosition() => hasOpenPosition == false

isLong() => tradeType == "LONG" ? true : false
isShort() => tradeType == "SHORT" ? true : false

if (isLong())

    crossedStopLoss = close <= stopLossPrice
    terminateOperation = hasOpenPosition and (crossedStopLoss or hasCloseLongConditional())

    if (terminateOperation)
        entryPrice := 0.0
        updatedEntryPrice := entryPrice
        stopLossPrice := 0.0
        hasOpenPosition := false
        alertClosePosition := true
    
    startOperation = notHasOpenPosition() and hasEntryLongConditional()

    if(startOperation)
        entryPrice := close
        updatedEntryPrice := entryPrice
        stopLossPrice := entryPrice + (entryPrice * stopLossPercent) / 100
        hasOpenPosition := true
        alertOpenPosition := true
        
    strategyPercentege = (close - updatedEntryPrice) / updatedEntryPrice * 100.00
    rideUpStopLoss = hasOpenPosition and strategyPercentege > 1

    if (rideUpStopLoss)
        stopLossPercent := stopLossPercent + strategyPercentege - 1.0
        newStopLossPrice = updatedEntryPrice + (updatedEntryPrice * stopLossPercent) / 100  
        stopLossPrice := max(stopLossPrice, newStopLossPrice)
        updatedEntryPrice := stopLossPrice


if (isShort())

    crossedStopLoss = close >= stopLossPrice
    terminateOperation = hasOpenPosition and (crossedStopLoss or hasCloseShortConditional())

    if (terminateOperation)
        entryPrice := 0.0
        updatedEntryPrice := entryPrice
        stopLossPrice := 0.0
        hasOpenPosition := false
        alertClosePosition := true
    
    startOperation = notHasOpenPosition() and hasEntryShortConditional()

    if(startOperation)
        entryPrice := close
        updatedEntryPrice := entryPrice
        stopLossPrice := entryPrice + (entryPrice * stopLossPercent) / 100
        hasOpenPosition := true
        alertOpenPosition := true
        
    strategyPercentege = (close - updatedEntryPrice) / updatedEntryPrice * 100.00
    rideDownStopLoss = hasOpenPosition and strategyPercentege < -1

    if (rideDownStopLoss)
        stopLossPercent := stopLossPercent + strategyPercentege + 1.0
        newStopLossPrice = updatedEntryPrice + (updatedEntryPrice * stopLossPercent) / 100  
        stopLossPrice := min(stopLossPrice, newStopLossPrice)
        updatedEntryPrice := stopLossPrice
    

entryPricePlotConditinal =  hasOpenPosition ? entryPrice : na
trailingStopLossPlotConditional =  hasOpenPosition ? stopLossPrice : na

plotshape(entryPricePlotConditinal, title= "Entry Price", color=color.blue, style=shape.circle, location=location.absolute, size=size.tiny)
plotshape(trailingStopLossPlotConditional, title= "Stop Loss", color=color.red, style=shape.circle, location=location.absolute, size=size.tiny)

alertcondition(alertOpenPosition, title="Open Position", message="Entry Position!")
alertcondition(alertClosePosition, title="Close Position", message="Close Position!")

alertOpenPosition := false
alertClosePosition := false
